plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
}

static def readProperty(project, name) {
    if (project.hasProperty(name)) {
        return project.getProperty(name)
    }
    println("Getting project property failed, returning null. Set $name as a project property in your ${project.projectDir}/gradle.properties")
    return null
}

static def readVersionCode(project) {
    def property = readProperty(project, "VERSION_CODE")
    if (property != null) {
        return property as int
    }
    return 1
}

static def readVersionName(project) {
    def property = readProperty(project, "VERSION_NAME")
    if (property != null) {
        return "$property"
    }
    return ""
}

android {
    namespace 'ch.nevis.exampleapp'
    compileSdk 34

    defaultConfig {
        applicationId "ch.nevis.exampleapp"
        minSdk 23
        targetSdk 34
        versionCode readVersionCode(this)
        versionName readVersionName(this)

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        configureEach {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = 17
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'

    // Barcode Scanning, to scan QR codes
    implementation 'com.google.mlkit:barcode-scanning:17.3.0'

    // CameraX
    def camerax_version = "1.3.4"
    implementation "androidx.camera:camera-core:${camerax_version}"
    implementation "androidx.camera:camera-camera2:${camerax_version}"
    implementation "androidx.camera:camera-lifecycle:${camerax_version}"

    implementation "androidx.camera:camera-view:${camerax_version}"
    implementation "androidx.camera:camera-extensions:${camerax_version}"

    // Dagger Hilt (dependency injection)
    implementation "com.google.dagger:hilt-android:$dagger_hilt_version"
    kapt "com.google.dagger:hilt-compiler:$dagger_hilt_version"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"

    // Nevis Mobile Authentication SDK
    implementation "ch.nevis:nevis-mobile-authentication-sdk-android-debug:$NEVIS_MOBILE_AUTHENTICATION_SDK_VERSION"

    // Logging
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Retrofit (HTTP requests)
    def retrofit_version = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
}
