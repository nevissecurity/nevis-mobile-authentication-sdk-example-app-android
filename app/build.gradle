plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.jetbrains.kotlin.android)
    alias(libs.plugins.jetbrains.kotlin.parcelize)
    alias(libs.plugins.jetbrains.kotlin.kapt)
    alias(libs.plugins.dagger.hilt.android)
    alias(libs.plugins.androidx.navigation.safeargs)
}

static def readProperty(project, name) {
    if (project.hasProperty(name)) {
        return project.getProperty(name)
    }
    println("Getting project property failed, returning null. Set $name as a project property in your ${project.projectDir}/gradle.properties")
    return null
}

static def readVersionCode(project) {
    def property = readProperty(project, "VERSION_CODE")
    if (property != null) {
        return property as int
    }
    return 1
}

static def readVersionName(project) {
    def property = readProperty(project, "VERSION_NAME")
    if (property != null) {
        return "$property"
    }
    return ""
}

android {
    namespace 'ch.nevis.exampleapp'
    compileSdk 34

    defaultConfig {
        applicationId "ch.nevis.exampleapp"
        minSdk 24
        targetSdk 34
        versionCode readVersionCode(this)
        versionName readVersionName(this)

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        configureEach {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = 11
    }
}

dependencies {
    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat
    implementation libs.material
    implementation libs.androidx.constraintlayout
    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.test.espresso.core

    // Barcode Scanning, to scan QR codes
    implementation libs.mlkit.barcode.scanning

    // CameraX
    implementation libs.androidx.camera.core
    implementation libs.androidx.camera.camera2
    implementation libs.androidx.camera.lifecycle

    implementation libs.androidx.camera.view
    implementation libs.androidx.camera.extensions

    // Dagger Hilt (dependency injection)
    implementation libs.dagger.hilt.android
    kapt libs.dagger.hilt.compiler

    // Navigation
    implementation libs.androidx.navigation.fragment.ktx
    implementation libs.androidx.navigation.ui.ktx

    // Nevis Mobile Authentication SDK
    implementation libs.nevis.mobile.authentication.sdk.android

    // Logging
    implementation libs.timber

    // Retrofit (HTTP requests)
    implementation libs.retrofit
    implementation libs.retrofit.converter.json
}
